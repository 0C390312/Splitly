plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.openapi.generator' version '7.2.0'
}

group = 'com.splitly'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jersey'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.liquibase:liquibase-core:4.25.1'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.2.1'
	implementation 'org.postgresql:postgresql:42.7.1'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.19'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.testcontainers:testcontainers:1.19.3'
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/openapi.yaml".toString()
	outputDir = "$buildDir/generated".toString()
	apiPackage = "com.splitly.core.api"
	modelPackage = "com.splitly.core.model"
	configOptions.set(
		dateLibrary: "java8",
		interfaceOnly: "true",
		useSpringBoot3: "true"
	)
}

sourceSets {
	main.java.srcDirs += "$buildDir/generated"
}

tasks.named('compileJava') {
	dependsOn 'openApiGenerate'
}

tasks.named('test') {
	useJUnitPlatform()
}
